// SPDX-License-Identifier: MIT
pragma solidity ^0.8.16;

import "@openzeppelin/contracts/token/ERC721/IERC721.sol";
import "@openzeppelin/contracts/token/ERC1155/IERC1155.sol";
import "@openzeppelin/contracts/utils/cryptography/ECDSA.sol";
import "@openzeppelin/contracts/token/ERC721/utils/ERC721Holder.sol";
import "@openzeppelin/contracts/token/ERC1155/utils/ERC1155Holder.sol";
import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";

import "./interfaces/ISodiumRegistry.sol";
import "./interfaces/ISodiumWallet.sol";
import "./interfaces/IWETH.sol";

contract SodiumWallet is ISodiumWallet, Initializable, ERC721Holder, ERC1155Holder {
    address private borrower;
    address private core;
    address private registry;
    IWETH private constant WETH = IWETH(0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2);

    modifier onlyCore() {
        require(msg.sender == core, "Sodium Wallet: Core only");
        _;
    }

    modifier onlyBorrower() {
        require(msg.sender == borrower, "Sodium Wallet: Borrower only");
        _;
    }

    function initialize(
        address borrower_,
        address core_,
        address registry_
    ) external override initializer {
        borrower = borrower_;
        core = core_;
        registry = registry_;
    }

    function execute(
        address[] calldata contractAddresses_,
        bytes[] memory calldatas_,
        uint256[] calldata values_
    ) external payable override onlyBorrower {
        for (uint256 i = 0; i < contractAddresses_.length; i++) {
            bytes memory cd = calldatas_[i];
            bytes4 signature;

            assembly {
                signature := mload(add(cd, 32))
            }

            require(
                ISodiumRegistry(registry).getCallPermission(contractAddresses_[i], signature),
                "Sodium Wallet: Non-permitted call"
            );

            (bool success, ) = contractAddresses_[i].call{value: values_[i]}(calldatas_[i]);

            require(success, "Sodium Wallet: Call failed");
        }
    }

    function transferERC721(
        address recipient_,
        address tokenAddress_,
        uint256 tokenId_
    ) external override onlyCore {
        IERC721(tokenAddress_).safeTransferFrom(address(this), recipient_, tokenId_);
    }

    function transferERC1155(
        address recipient_,
        address tokenAddress_,
        uint256 tokenId_
    ) external override onlyCore {
        IERC1155(tokenAddress_).safeTransferFrom(address(this), recipient_, tokenId_, 1, "");
    }

    /// @notice EIP-1271 implementation that validates signatures generated by the borrower
    function isValidSignature(bytes32 hash, bytes memory signature) external view returns (bytes4) {
        address signer = ECDSA.recover(hash, signature);

        require(signer == borrower, "Sodium Wallet: Signature must be made by borrower ");

        return 0x1626ba7e;
    }

    function withdrawETH(uint256 amount) external payable onlyBorrower {
        payable(msg.sender).transfer(amount);
    }

    function withdrawWETH(uint256 amount) external onlyBorrower {
        WETH.transfer(msg.sender, amount);
    }

    receive() external payable {}
}
